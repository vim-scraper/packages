" gotofile.vim: gf replacement by using the tags file
" Author: Nathan Huizinga (nathan dot huizinga at gmail dot com)
" Last Change: 02-Mar-2007
" Created:     01-Mar-2007
" Requires:    Vim-7.0, genutils.vim(1.2)
" Version:     0.1
" Licence: This program is free software; you can redistribute it and/or
"          modify it under the terms of the GNU General Public License.
"          See http://www.gnu.org/copyleft/gpl.txt 
" Download From:
"     http://www.vim.org//script.php?script_id=xxxx
" Usage:
"     This script defines a new command 'GotoFile', which resembles the normal
"     'gf' function, but uses a standard tags file generated by ctags for
"     looking up the file name under the cursor. 
"
"	  You need to generate a standard tags file (with ctags) for this script to work.
"	  Add the following ctags option for generating filename tags: --extra=+f
"
"	  For example: ctags -R --extra=+f
"
"	  Add the following mapping to your .vimrc (or _vimrc) to replace the
"	  normal 'gf' behavior by the GotoFile command:
"
"	  nmap <silent> gf :GotoFile<CR>
"
"     I wrote this script mainly for having an alternative for setting the
"     path and using 'gf'. While using completion all header files within
"     the path are also searched, which causes a major performance hit for
"     large projects (at least for me). So this script allows using the
"     'gf' feature without setting the path. Other suggestions are welcome!
"
" Installing:
"     Just place this script in your plugin directory.
"     Generate the tags file.
"     Make sure that your tags file can be found by vim. See :help 'tags'
"     Install the latest version of genutils
"     (http://www.vim.org/scripts/script.php?script_id=197).
"

if exists("loaded_gotofile")
    finish
endif
if v:version < 700
  echomsg 'gotofile: You need at least Vim 7.0'
  finish
endif
if !exists('loaded_genutils')
  runtime plugin/genutils.vim
endif
if !exists('loaded_genutils') || loaded_genutils < 200
  echomsg 'gotofile: You need a newer version of genutils.vim plugin'
  finish
endif
let loaded_gotofile = 1

command! -nargs=0 -complete=file GotoFile :call <SID>FindFile(expand("<cfile>:t"))

function! s:FindFile(pattern)
	if a:pattern == ""
        echohl ErrorMsg | echo "No filename!" | echohl NONE
		return
	endif

	try
		let tags = taglist(a:pattern)
	catch
        echohl ErrorMsg | echo "Exception: " . v:exception | echohl NONE
		return
	endtry

	let files = map(tags, 'v:val["filename"]')
	if len(files) == 0
        echohl ErrorMsg | echo "File not found!" | echohl NONE
		return
	endif

	if len(files) > 1
        echohl ErrorMsg | echo "Multiple matches found!" | echohl NONE
		return
	endif

	let fileName = files[0]

	" [START] code snippet taken from: lookupfile.vim
	"     http://www.vim.org//script.php?script_id=1581
	let winnr = bufwinnr(genutils#FindBufferForName(fileName))
	if winnr != -1
		exec winnr.'wincmd w'
	else
		let splitOpen = 0
		if &switchbuf ==# 'split'
			let splitOpen = 1
		endif
		" First try opening as a buffer, if it fails, we will open as a file.
		try
			exec (splitOpen?'s':'').'buffer' fileName
		catch
			exec (splitOpen?'split':'edit') fileName
		endtry
	endif
	" [END] code snippet
endfunction

" vim6:sw=4
